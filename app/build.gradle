apply plugin: 'android'
apply plugin: 'witness'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        free {
            applicationId 'net.i2p.android'
        }
        donate {
            applicationId 'net.i2p.android.donate'
        }
        legacy {
            applicationId 'net.i2p.android.router'
        }
    }
}

dependencies {
    compile project(':routerjars')
    compile project(':client')
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile files('libs/androidplot-core-0.6.1.jar')
}

dependencyVerification {
    verify = [
            'com.android.support:support-v4:199ef7bb169386c80b4836354df6747ce2ae3d24434db923c22439e47106a1e2',
            'com.android.support:appcompat-v7:45e999dda55fe81d9cc1c7342b7b70480ff3f307baa8da0df767f92fc5c52cd1',
            'com.android.support:recyclerview-v7:ab2390d688601b65e2f3a0718b3d25487e61546c4e20f81eb0b033f30ca15b31',
            'com.android.support:cardview-v7:7b724eb46efc98eee70c333cd0e9c34ca89b1a4456c3e40cfcc33501c43570bf',
    ]
}

project.ext.i2pbase = '../i2p.i2p'
def Properties props = new Properties()
def propFile = new File(project(':routerjars').projectDir, 'local.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('i2psrc')) {
        i2pbase = props['i2psrc']
    } else {
        println 'local.properties found but some entries are missing'
    }
} else {
    println 'local.properties not found'
}

task certificatesZip(type: Zip) {
    archiveName = 'certificates_zip'
    from files('' + i2pbase + '/installer/resources/certificates')
}
task copyI2PResources(type: Copy) {
    into 'src/main'
    into('res/drawable') {
        from file(i2pbase + '/installer/resources/themes/console/images/i2plogo.png')
    }
    into('res/raw') {
        from(i2pbase + '/installer/resources/blocklist.txt') { rename {'blocklist_txt' } }
        from(i2pbase + '/installer/resources/hosts.txt') { rename {'hosts_txt' } }
        from('../LICENSE.txt') { rename {'license_app_txt' } }
        from('../licenses/LICENSE-Apache2.0.txt') { rename {'license_apache20_txt' } }
        from(i2pbase + '/licenses') {
            include { elem ->
                elem.name in [
                        'LICENSE-ElGamalDSA.txt',
                        'LICENSE-SHA256.txt',
                        'LICENSE-BSD.txt',
                        'LICENSE-SNTP.txt',
                        'LICENSE-LGPLv2.1.txt',
                        'LICENSE-InstallCert.txt',
                        'LICENSE-BlockFile.txt',
                        'LICENSE-GPLv2.txt',
                        'LICENSE-GPLv3.txt',
                        'LICENSE-LGPLv3.txt',
                        'LICENSE-FatCowIcons.txt',
                        'LICENSE-Addressbook.txt',
                ]
            }
            rename { String name ->
                String part = name.substring(8, name.lastIndexOf('.txt'))
                String.format('license_%s_txt',
                        part.toLowerCase(Locale.US).replace('.', '_'))
            }
        }
        from certificatesZip
    }
    // For peers WebView
    into('assets/themes/console/images') {
        from file(i2pbase + '/installer/resources/themes/console/images/i2plogo.png')
        from file(i2pbase + '/installer/resources/themes/console/images/inbound.png')
        from file(i2pbase + '/installer/resources/themes/console/images/outbound.png')
    }
    into ('assets/themes/console/light') {
        from file(i2pbase + '/installer/resources/themes/console/light/console.css')
    }
    into('assets/themes/console/light/images') {
        from file(i2pbase + '/installer/resources/themes/console/light/images/header.png')
    }
}
task cleanI2PResources(type: Delete) {
    delete file('src/main/res/drawable/i2plogo.png')
    delete fileTree('src/main/res/raw') {
        include 'blocklist_txt'
        include 'hosts_txt'
        include 'license_*'
        include 'certificates_zip'
    }
    delete fileTree('src/main/assets/themes/console/images')
    delete file('src/main/assets/themes/console/light/console.css')
    delete file('src/main/assets/themes/console/light/images/header.png')
}
preBuild.dependsOn copyI2PResources
clean.dependsOn cleanI2PResources

props = new Properties()
propFile = new File(project.rootDir, 'signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

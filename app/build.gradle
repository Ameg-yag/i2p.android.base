apply plugin: 'com.android.application'
apply plugin: 'witness'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION as String)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION as String
    defaultConfig {
        versionCode 4745234
        versionName '0.9.27'
        minSdkVersion 9
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION as String)

        // For Espresso
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    productFlavors {
        free {
            applicationId 'net.i2p.android'
        }
        donate {
            applicationId 'net.i2p.android.donate'
        }
        legacy {
            applicationId 'net.i2p.android.router'
        }
    }
}

dependencies {
    // Local dependencies
    compile project(':lib:client')
    compile project(':lib:helper')
    compile project(':routerjars')

    // Android Support Repository dependencies
    def supportVersion = '25.0.1'
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"

    // Remote dependencies
    compile 'com.androidplot:androidplot-core:0.9.8'
    compile 'com.eowise:recyclerview-stickyheaders:0.5.2@aar'
    compile ('com.mcxiaoke.viewpagerindicator:library:2.4.1') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'net.i2p:router:0.9.28'
    compile 'net.i2p.android.ext:floatingactionbutton:1.10.1'
    compile 'org.sufficientlysecure:html-textview:1.6'

    // Testing-only dependencies
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

dependencyVerification {
    verify = [
            'com.android.support:support-v4:50da261acc4ca3d2dea9a43106bf65488711ca97b20a4daa095dba381c205c98',
            'com.android.support:appcompat-v7:7fead560a22ea4b15848ce3000f312ef611fac0953bf90ca8710a72a1f6e36ea',
            'com.android.support:preference-v7:d7e3fcb6d5427aa25bfd56c51c24786dbb6f06d998b8b6691e9449e1b11cc205',
            'com.android.support:preference-v14:9d0269913033d97d8edb29003e1ea19021f2e8f36df4035f819bb948f9a23ed2',
            'com.android.support:recyclerview-v7:803baba7be537ace8c5cb8a775e37547c22a04c4b028833796c45c26ec1deca2',
            'com.androidplot:androidplot-core:e44d9e59e06f025330831f7d3c987d2778a3302025184cf0cef05714b5171212',
            'com.eowise:recyclerview-stickyheaders:7b236da49b33b840e9ba6e7e4182218d1a2d9047236fdbc3ca947352f9b0883b',
            'com.mcxiaoke.viewpagerindicator:library:1e8aad664137f68abdfee94889f6da3dc98be652a235176a403965a07a25de62',
            'com.pnikosis:materialish-progress:da089a90d1dab61e9b50038c09081019398f81190d12b0b567ce94b83ef8cf93',
            'net.i2p:router:de3cf0a0e99823662c938d6a1083f201f8feba7d0ebebaf3179fed7040863b7c',
            'net.i2p.android.ext:floatingactionbutton:09d43e2d4ac04a91bf7a37e1ec48a8d220204e3a55dca72cd36cd9fa27461ade',
            'org.sufficientlysecure:html-textview:c409b471618b675e3d2a8588f883c5fe8f3369d00df61ec84b29f29c648370ae',
    ]
}

project.ext.i2pbase = '../i2p.i2p'
def Properties props = new Properties()
def propFile = new File(project(':routerjars').projectDir, 'local.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('i2psrc')) {
        i2pbase = props['i2psrc']
    } else {
        println 'local.properties found but some entries are missing'
    }
} else {
    println 'local.properties not found'
}

task certificatesZip(type: Zip) {
    archiveName = 'certificates_zip'
    from files('' + i2pbase + '/installer/resources/certificates')
}
task copyI2PResources(type: Copy) {
    // Force this to always run: Copy only detects source changes, not if missing in destination
    outputs.upToDateWhen { false }
    into 'src/main/res'
    into('drawable') {
        from file(i2pbase + '/installer/resources/themes/console/images/i2plogo.png')
    }
    into('raw') {
        from(i2pbase + '/installer/resources/blocklist.txt') { rename { 'blocklist_txt' } }
        from(i2pbase + '/installer/resources/hosts.txt') { rename { 'hosts_txt' } }
        from(i2pbase + '/installer/resources/proxy') {
            include { elem ->
                elem.name.endsWith('.ht')
            }
            rename { String name ->
                name.toLowerCase(Locale.US).replace('-', '_').replace('.', '_')
            }
            filter { String line ->
                // Remove links to routerconsole
                def m = line =~ /127.0.0.1:7657/
                if (m.getCount()) {
                    // Links around content
                    line = line.replaceAll(/<a href="http:\/\/127.0.0.1:7657[^>]*>(.+?)<\/a>/) { fullmatch, content ->
                        content
                    }
                    // Links in translation substitutions
                    line = line.replaceAll(/"<a href=\\"http:\/\/127.0.0.1:7657[^>]*>", "<\/a>"/, '"", ""')
                }
                // Remove "Configuration - Help - Addressbook" heading
                def n = line =~ /Configuration.+Help.+Addressbook/
                if (n.getCount())
                    ""
                else
                    line
            }
        }
        from('../LICENSE.txt') { rename { 'license_app_txt' } }
        from('../licenses/LICENSE-Apache2.0.txt') { rename { 'license_apache20_txt' } }
        from(i2pbase + '/licenses') {
            include { elem ->
                elem.name in [
                        'LICENSE-ElGamalDSA.txt',
                        'LICENSE-SHA256.txt',
                        'LICENSE-BSD.txt',
                        'LICENSE-SNTP.txt',
                        'LICENSE-LGPLv2.1.txt',
                        'LICENSE-InstallCert.txt',
                        'LICENSE-BlockFile.txt',
                        'LICENSE-GPLv2.txt',
                        'LICENSE-GPLv3.txt',
                        'LICENSE-LGPLv3.txt',
                        'LICENSE-FatCowIcons.txt',
                        'LICENSE-Addressbook.txt',
                ]
            }
            rename { String name ->
                name.toLowerCase(Locale.US).replace('-', '_').replace('.', '_')
            }
        }
        from certificatesZip
    }
}
// For peers WebView
task copyI2PAssets(type: Copy) {
    // Force this to always run: Copy only detects source changes, not if missing in destination
    outputs.upToDateWhen { false }
    into 'src/main/assets/themes/console'
    into('images') {
        from file(i2pbase + '/installer/resources/themes/console/images/i2plogo.png')
        from file(i2pbase + '/installer/resources/themes/console/images/inbound.png')
        from file(i2pbase + '/installer/resources/themes/console/images/outbound.png')
    }
    into('light') {
        from file(i2pbase + '/installer/resources/themes/console/light/console.css')
    }
    into('light/images') {
        from file(i2pbase + '/installer/resources/themes/console/light/images/header.png')
    }
}

preBuild.dependsOn copyI2PResources
preBuild.dependsOn copyI2PAssets

task cleanI2PResources(type: Delete) {
    delete file('src/main/res/drawable/i2plogo.png')
    delete fileTree('src/main/res/raw') {
        include 'blocklist_txt'
        include 'hosts_txt'
        include '*_ht'
        include 'license_*'
        include 'certificates_zip'
    }
}
task cleanI2PAssets(type: Delete) {
    delete fileTree('src/main/assets/themes/console/images')
    delete file('src/main/assets/themes/console/light/console.css')
    delete file('src/main/assets/themes/console/light/images/header.png')
}

clean.dependsOn cleanI2PResources
clean.dependsOn cleanI2PAssets

props = new Properties()
propFile = new File(project.rootDir, 'signing.properties')

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

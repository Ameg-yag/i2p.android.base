apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "net.i2p.android"
version = "0.1"

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId group
        minSdkVersion 9
        targetSdkVersion 19
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile project(path: ':routerjars', configuration: 'client')
}

android.libraryVariants.all { variant ->
    // Copy I2P client JARs into AAR
    def copyClientLibsTask = tasks.create(name: "copy${variant.name.capitalize()}ClientLibs", type: Copy) {
        from project(':routerjars').configurations.client.allArtifacts.collect { artifact ->
            artifact.file
        }
        destinationDir file("${buildDir}/intermediates/bundles/${variant.dirName}/libs")
    }

    // Hook the copy task into the build
    variant.packageLibrary.dependsOn copyClientLibsTask
    copyClientLibsTask.mustRunAfter ':routerjars:buildClient'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'I2P Android client library'
                packaging 'aar'
                description 'A client library enabling Android developers to easily add I2P Android support to their apps.'
                url 'https://geti2p.net'
                inceptionYear 2014

                scm {
                    connection 'https://github.com/i2p/i2p.android.base.git'
                    url 'https://github.com/i2p/i2p.android.base'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                organization {
                    name 'I2P'
                    url 'https://geti2p.net'
                }

                developers {
                    developer {
                        id 'str4d'
                        name 'str4d'
                        email 'str4d@i2pmail.org'
                        organization 'I2P'
                        organizationUrl 'https://geti2p.net'
                    }
                }
            }
        }
    }
}

def deployer = uploadArchives.repositories.mavenDeployer
deployer*.pom*.whenConfigured {pom ->
    pom.setDependencies(pom.dependencies.collect { dep ->
        if (dep.groupId != 'i2p.android.base' && dep.artifactId != 'routerjars')
            dep
    })
}
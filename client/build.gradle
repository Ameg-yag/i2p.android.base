apply plugin: 'com.android.library'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile project(path: ':routerjars', configuration: 'client')
}

android.libraryVariants.all { variant ->
    // Copy I2P client JARs into AAR
    def copyClientLibsTask = tasks.create(name: "copy${variant.name.capitalize()}ClientLibs", type: Copy) {
        from project(':routerjars').configurations.client.allArtifacts.collect { artifact ->
            artifact.file
        }
        destinationDir file("${buildDir}/intermediates/bundles/${variant.dirName}/libs")
    }

    // Hook the copy task into the build
    variant.packageLibrary.dependsOn copyClientLibsTask
    copyClientLibsTask.mustRunAfter ':routerjars:buildClient'
}

if (hasProperty('localFileRepoDir')) {
    uploadArchives {
        repositories {
            add project.repositories.fileRepo
        }
    }
} else {
    apply from: "${project.rootDir}/gradle/maven-push.gradle"

    // Remove routerjars from dependencies
    afterEvaluate { project ->
        def deployer = uploadArchives.repositories.mavenDeployer
        deployer*.pom*.whenConfigured { pom ->
            pom.setDependencies(pom.dependencies.collect { dep ->
                if (dep.groupId != 'i2p.android.base' && dep.artifactId != 'routerjars')
                    dep
            })
        }
    }
}
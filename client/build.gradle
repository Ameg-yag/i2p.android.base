apply plugin: 'com.android.library'
apply plugin: 'witness'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION as String)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION as String)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(path: ':routerjars', configuration: 'client')
    compile 'com.android.support:support-v4:23.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

dependencyVerification {
    verify = [
            'com.android.support:support-v4:5c7dceb6c824089fe80f502e5206264048ef8bffa4e8ddeab180b81723e79b7f',
    ]
}

android.libraryVariants.all { variant ->
    // Copy I2P client JARs into AAR
    def copyClientLibsTask = tasks.create(name: "copy${variant.name.capitalize()}ClientLibs", type: Copy) {
        from project(':routerjars').configurations.client.allArtifacts.collect { artifact ->
            artifact.file
        }
        destinationDir file("${buildDir}/intermediates/bundles/${variant.dirName}/libs")
    }

    // Hook the copy task into the build
    variant.processJavaResources.dependsOn copyClientLibsTask
    copyClientLibsTask.mustRunAfter ':routerjars:buildClient'
}

apply from: "${project.rootDir}/gradle/maven-push.gradle"

// Remove routerjars from dependencies
afterEvaluate { project ->
    def deployer = uploadArchives.repositories.mavenDeployer
    deployer*.pom*.whenConfigured { pom ->
        pom.setDependencies(pom.dependencies.collect { dep ->
            if (dep.groupId != 'i2p.android.base' && dep.artifactId != 'routerjars')
                dep
        })
    }
}

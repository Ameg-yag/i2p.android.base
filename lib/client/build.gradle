apply plugin: 'com.android.library'
apply plugin: 'witness'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION as String)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION as String)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree('libs') {
        builtBy 'buildClient'
        include 'i2p.jar'
        include 'mstreaming.jar'
        include 'streaming.jar'
    }
    // No direct dependencies on the helper lib, but this ensures consistent library behavior
    compile project(':lib:helper')
    compile 'com.android.support:support-v4:23.2.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

dependencyVerification {
    verify = [
            'com.android.support:support-v4:81ce890f26d35c75ad17d0f998a7e3230330c3b41e0b629566bc744bee89e448',
    ]
}

task buildClient(type: Exec) {
    outputs.dir('libs')
    commandLine 'ant', 'buildclient'
}

clean.dependsOn cleanBuildClient

clean << {
    exec {
        commandLine 'ant', 'clean'
    }
}

apply from: "${project.rootDir}/gradle/maven-push.gradle"
